use crate::{
    parser::Context,
    ir::{
        types::{self, Type, ArrayType},
        util::unescape,
        module::{attributes::Attribute, name::Name}
    }
};

grammar(ctx: &mut Context);

pub Module: () = {
    <x:ModuleSub *> => {
      ()
    }
};

ModuleSub: () = {
    "source_filename" "=" <name:StringLiteral> => ctx.module.source_filename = name,
    "target" "datalayout" "=" <dl:StringLiteral> => ctx.module.target.datalayout = dl.into(),
    "target" "triple" "=" <tr:StringLiteral> => ctx.module.target.triple = tr.into(),
    <attr_group:AttributeGroup> => { ctx.module.attributes.insert(attr_group.0, attr_group.1); },
    <name:LocalTypeName> "=" "type" <t:Type> =>
        ctx.module.types.base_mut().change_to_named_type(t, name),
}

LocalTypeName: Name = {
    <name:LocalName> => {
        ctx.module.types.base_mut().empty_named_type(name.clone());
        name
    }
}

Type: Type = {
    <t:Type> "*" => ctx.module.types.base_mut().pointer(t),
    "[" <n:r"[0-9]+"> "x" <t:Type> "]" => ctx.module.types.base_mut().array(ArrayType::new(t, n.parse().unwrap())),
    <t:PrimitiveType> => t,
}

PrimitiveType: Type = {
    "metadata" => ctx.module.types.metadata(),
    "void" => types::VOID,
    "i64" => types::I64,
    "i32" => types::I32,
    "i16" => types::I16,
    "i8" => types::I8,
    "i1" => types::I1,
}

LocalName: Name = {
    <s:r#"%".*""#> => Name::Name(unescape(s.trim_start_matches("%\"").trim_end_matches("\"")).unwrap()),
    <s:r#"%[a-zA-Z][a-zA-Z0-9\._]*"#> => Name::Name(s.trim_start_matches("%").to_owned()),
    <s:r#"%[0-9]+"#> => Name::Number(s.parse().unwrap()),
}

AttributeGroup: (u32, Vec<Attribute>) = {
    "attributes" <id:r"#[0-9]+"> "=" "{" <attrs:Attribute *> "}" =>
        (id.trim_start_matches('#').parse().unwrap(), attrs)
}

Attribute: Attribute = {
    "alwaysinline"                => Attribute::AlwaysInline,
    "builtin"                     => Attribute::Builtin,
    "cold"                        => Attribute::Cold,
    "convergent"                  => Attribute::Convergent,
    "inaccessiblememonly"         => Attribute::InaccessibleMemOnly,
    "inaccessiblememorargmemonly" => Attribute::InaccessibleMemOrArgMemOnly,
    "inlinehint"                  => Attribute::InlineHint,
    "jumptable"                   => Attribute::JumpTable,
    "minimizesize"                => Attribute::MinimizeSize,
    "mustprogress"                => Attribute::MustProgress,
    "naked"                       => Attribute::Naked,
    "nobuiltin"                   => Attribute::NoBuiltin,
    "nocfcheck"                   => Attribute::NoCFCheck,
    "noduplicate"                 => Attribute::NoDuplicate,
    "nofree"                      => Attribute::NoFree,
    "noimplicitfloat"             => Attribute::NoImplicitFloat,
    "noinline"                    => Attribute::NoInline,
    "nonlazybind"                 => Attribute::NonLazyBind,
    "noredzone"                   => Attribute::NoRedZone,
    "noreturn"                    => Attribute::NoReturn,
    "norecurse"                   => Attribute::NoRecurse,
    "willreturn"                  => Attribute::WillReturn,
    "returnstwice"                => Attribute::ReturnsTwice,
    "nosync"                      => Attribute::NoSync,
    "nounwind"                    => Attribute::NoUnwind,
    "optforfuzzing"               => Attribute::OptForFuzzing,
    "optnone"                     => Attribute::OptNone,
    "optsize"                     => Attribute::OptSize,
    "readnone"                    => Attribute::ReadNone,
    "readonly"                    => Attribute::ReadOnly,
    "writeonly"                   => Attribute::WriteOnly,
    "argmemonly"                  => Attribute::ArgMemOnly,
    "safestack"                   => Attribute::SafeStack,
    "sanitizeaddress"             => Attribute::SanitizeAddress,
    "sanitizememory"              => Attribute::SanitizeMemory,
    "sanitizethread"              => Attribute::SanitizeThread,
    "sanitizehwaddress"           => Attribute::SanitizeHWAddress,
    "sanitizememtag"              => Attribute::SanitizeMemTag,
    "shadowcallstack"             => Attribute::ShadowCallStack,
    "speculativeloadhardening"    => Attribute::SpeculativeLoadHardening,
    "speculatable"                => Attribute::Speculatable,
    "ssp"                         => Attribute::StackProtect,
    "sspreq"                      => Attribute::StackProtectReq,
    "sspstrong"                   => Attribute::StackProtectStrong,
    "strictfp"                    => Attribute::StrictFP,
    "uwtable"                     => Attribute::UWTable,
}

StringLiteral: String = {
    <s:r###"".*""###> => unescape(s.trim_matches('"')).unwrap(),
}

